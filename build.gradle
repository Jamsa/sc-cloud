//依赖版本号
ext.versions = [
        spring: '4.3.14.RELEASE',
        springCloud :'1.3.2.RELEASE',
        springBoot : '1.5.10.RELEASE',
        netflix:'1.4.3.RELEASE'
]
//依赖
ext.libs = [
        "spring-cloud":"org.springframework.cloud:spring-cloud-starter:${versions.springCloud}",
        "spring-boot":"org.springframework.boot:spring-boot-starter:${versions.springBoot}",
        "spring-web":"org.springframework:spring-web:${versions.spring}",
        "eureka-server":"org.springframework.cloud:spring-cloud-starter-netflix-eureka-server:${versions.netflix}",
        "eureka-client":"org.springframework.cloud:spring-cloud-starter-netflix-eureka-server:${versions.netflix}",
        "zuul":"org.springframework.cloud:spring-cloud-starter-netflix-zuul:${versions.netflix}",
        "http-client":"org.apache.httpcomponents:httpclient:4.5.2"
]
//只起分类作用的目录
ext.excludeFolds = ["demo0","demo1"]

buildscript {
    ext {
        springIOVersion = '1.0.5.RELEASE'
        springBootVersion = '1.5.10.RELEASE'
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${springIOVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

subprojects {
    //对分类目录不需要进行配置
    if(excludeFolds.contains(name)) return

    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        maven { url "https://oss.sonatype.org/content/groups/public/" }
        maven { url "https://repo.spring.io/libs-milestone/" }
        mavenCentral()
    }

    apply plugin: 'java'

    //api工程不需要使用spring boot插件
    if(!name.contains("api")) {
        apply plugin: 'org.springframework.boot'
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    group = 'org.github.jamsa.sc'
    version = '0.0.1'
    if(name=='api'){
        dependencies {
            compile libs.'spring-web'
        }
    }

    dependencies {
        //compile libs.'spring-web'
        //compile libs.'spring-boot'
        compile libs.'http-client'
    }

    //设置打包参数
    tasks.withType(Jar) {
        //包名为 sc-项目名-模块名，如果父项目为根项目，则不添加“项目名”
        archivesBaseName = "sc-"+(project.parent==rootProject?project.name:project.parent.name+"-"+project.name)
        baseName = archivesBaseName
    }
}


